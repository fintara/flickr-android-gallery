apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.tsovedenski.flickrgallery"
        minSdkVersion 25
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions.unitTests.all {
        useJUnitPlatform()

        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }

        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest result: ${result.resultType}"
                println "Test summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
            }
        }

    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlin_coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlin_coroutine"

    implementation "androidx.appcompat:appcompat:$versions.androidx_appcompat"
    implementation "androidx.recyclerview:recyclerview:$versions.androidx_recyclerview"
    implementation "androidx.cardview:cardview:$versions.androidx_cardview"
    implementation "androidx.constraintlayout:constraintlayout:$versions.androidx_constraintlayout"

    implementation "androidx.lifecycle:lifecycle-extensions:$versions.androidx_lifecycle"
    implementation "androidx.fragment:fragment:$versions.androidx_fragment"
    implementation "androidx.fragment:fragment-ktx:$versions.androidx_fragment"

    implementation "com.github.bumptech.glide:glide:$versions.glide"
//    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$versions.retrofit_adapter"

    implementation "com.github.MikeOrtiz:TouchImageView:$versions.touch_imageview"

    testImplementation "io.mockk:mockk:$versions.mockk"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$versions.kotlin"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$versions.junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$versions.junit"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$versions.junit"

    androidTestImplementation "com.android.support.test:runner:$versions.support_test"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espresso"
}
